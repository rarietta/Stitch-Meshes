

//======================================================================================================================//
// Loop Selection Build Function																						//
//======================================================================================================================//

global proc AELoopSelectionBuild (string $nodeNameArr) {
	
	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];
	
	frameLayout -marginHeight 5 -marginWidth 5 -collapsable 0 -labelVisible 0 -enable true LoopSelectionFrame;

		//--------------------------------------------------------------------------------------------------------------//
		// User instructions / description																				//
		//--------------------------------------------------------------------------------------------------------------//

		separator -style none;
		text -l "To specify the stitch direction of your mesh, first select a boundary";
		text -l "edge loop at which to begin the knitting. Then, select contiguous edge";
		text -l "loops in sequence, or start knitting again from another boundary.";
		separator -style none;

	//------------------------------------------------------------------------------------------------------------------//
	// Attribute controls and buttons																					//
	//------------------------------------------------------------------------------------------------------------------//

	string $commitCmd = "setAttr " + $nodeName + ".nodeStage 1; \
							hide pCylinder1; showHidden StitchMeshShape; \
							editorTemplate -dimControl " + $nodeName + " stitchSize false; \
							button -e -enable true CommitTessellationButton; \
							button -e -enable true ReturnTessellationButton; \
							button -e -enable false CommitLoopButton; \
							button -e -enable false ReturnLoopButton; \
							frameLayout -e -enable false LoopSelectionFrame;";
	string $clearCmd = "select " + $nodeName + ".inputMesh; \
						polyColorPerVertex -remove;";
	button -label "Commit Stitch Loop Selection" -c $commitCmd CommitLoopButton;
	button -label "Clear Stitch Loop Selection" -c $clearCmd ReturnLoopButton;
}

//======================================================================================================================//
// Loop Selection Update Function																						//
//======================================================================================================================//

global proc AELoopSelectionUpdate (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];
	
	// dim/undim attribute fields based on nodeStage
	int $stage = `getAttr ($nodeName + ".nodeStage")`;
	if ($stage == 0) {
		button -e -enable true CommitLoopButton;
		button -e -enable true ReturnLoopButton;
		frameLayout -e -enable true LoopSelectionFrame;
	} else {
		button -e -enable false CommitLoopButton;
		button -e -enable false ReturnLoopButton;
		frameLayout -e -enable false LoopSelectionFrame;
	}

	// add buttons
	string $cmd = "setAttr " + $nodeName + ".nodeStage 1; \
				   hide pCylinder1; showHidden StitchMeshShape; \
				   editorTemplate -dimControl " + $nodeName + " stitchSize false; \
				   button -e -enable true CommitTessellationButton; \
				   button -e -enable true ReturnTessellationButton; \
				   button -e -enable false CommitLoopButton; \
				   button -e -enable false ReturnLoopButton; \
				   frameLayout -e -enable false LoopSelectionFrame;";
	button -e -c $cmd CommitLoopButton;
}

//======================================================================================================================//
// Tessellation Build Function																							//
//======================================================================================================================//

global proc AETessellationBuild (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

	//------------------------------------------------------------------------------------------------------------------//
	// Attribute controls and buttons																					//
	//------------------------------------------------------------------------------------------------------------------//
	
	//text -l "This slider controls the average stitch size of the knitted output.";
	editorTemplate -dimControl $nodeName stitchSize true;
	string $commitCmd = "setAttr " + $nodeName + ".nodeStage 2; \
						 editorTemplate -dimControl " + $nodeName + " stitchSize true; \
					     button -e -enable false CommitTessellationButton; \
					     button -e -enable false ReturnTessellationButton; \
					     button -e -enable true CommitEditButton; \
						 frameLayout -e -enable true StitchLevelEditing; \
					     button -e -enable true ReturnEditButton;";
	string $returnCmd = "setAttr " + $nodeName + ".nodeStage 0; \
						 showHidden pCylinder1; hide StitchMeshShape; \
						 editorTemplate -dimControl " + $nodeName + " stitchSize true; \
						 button -e -enable false CommitTessellationButton; \
					     button -e -enable false ReturnTessellationButton; \
					     button -e -enable true CommitLoopButton; \
						 frameLayout -e -enable true LoopSelectionFrame; \
					     button -e -enable true ReturnLoopButton;";
	button -label "Commit and Edit Stitch Mesh"   -enable false -c $commitCmd CommitTessellationButton;
	button -label "Return to Edge Loop Selection" -enable false -c $returnCmd ReturnTessellationButton;
}

//======================================================================================================================//
// Tessellation Update Function																							//
//======================================================================================================================//

global proc AETessellationUpdate (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

	// dim/undim attribute fields based on nodeStage
	int $stage = `getAttr ($nodeName + ".nodeStage")`;
	if ($stage == 1) {
		editorTemplate -dimControl $nodeName stitchSize false;
		button -e -enable true CommitTessellationButton;
		button -e -enable true ReturnTessellationButton;
	} else { 
		editorTemplate -dimControl $nodeName stitchSize true;
		button -e -enable false CommitTessellationButton;
		button -e -enable false ReturnTessellationButton;
	}

	// update button commands
	string $commitCmd = "setAttr " + $nodeName + ".nodeStage 2; \
					     button -e -enable false CommitTessellationButton; \
					     button -e -enable false ReturnTessellationButton; \
					     button -e -enable true CommitEditButton; \
					     button -e -enable true ReturnEditButton; \
						 frameLayout -e -enable true StitchLevelEditing; \
						 editorTemplate -dimControl " + $nodeName + " stitchSize true;";
	string $returnCmd = "setAttr " + $nodeName + ".nodeStage 0; \
						 showHidden pCylinder1; hide StitchMeshShape; \
						 button -e -enable false CommitTessellationButton; \
					     button -e -enable false ReturnTessellationButton; \
					     button -e -enable true CommitLoopButton; \
					     button -e -enable true ReturnLoopButton; \
						 frameLayout -e -enable true LoopSelectionFrame; \
						 editorTemplate -dimControl " + $nodeName + " stitchSize true;";
	button -e -c $commitCmd CommitTessellationButton;
	button -e -c $returnCmd ReturnTessellationButton;
}	

//======================================================================================================================//
// Stitch Editing Build Function																						//
//======================================================================================================================//

global proc AEStitchEditBuild (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];
	
	string $commitCmd = "setAttr " + $nodeName + ".nodeStage 3; \
						 frameLayout -e -enable false StitchLevelEditing; \
					     button -e -enable false CommitEditButton; \
					     button -e -enable false ReturnEditButton; \
						 showHidden pCylinder1; select -r pCylinder1; scale 0.90;";
	string $returnCmd = "showHidden pCylinder1; \
						 setAttr " + $nodeName + ".nodeStage 1; \
						 frameLayout -e -enable false StitchLevelEditing; \
						 button -e -enable true CommitTessellationButton; \
					     button -e -enable true ReturnTessellationButton; \
					     button -e -enable false CommitEditButton; \
					     button -e -enable false ReturnEditButton; \
						 editorTemplate -dimControl " + $nodeName + " stitchSize false; \
						 hide pCylinder1; showHidden StitchMeshShape;";

	//------------------------------------------------------------------------------------------------------------------//
	// Upper Level Stitch Editing Frame   																				//
	//------------------------------------------------------------------------------------------------------------------//

	string $path = "C:/Users/Richard/Documents/_Academics/Penn/02_S2014/CIS_660/AuthoringTool/StitchMeshes/icons/";
	frameLayout -marginHeight 5 -marginWidth 5 -collapsable 0 -labelVisible 0 -enable false StitchLevelEditing;

		//--------------------------------------------------------------------------------------------------------------//
		// Description/Instructions																						//
		//--------------------------------------------------------------------------------------------------------------//
		separator -style none;
		text -l "To perform stitch level editing, select the desired edge(s)";
		text -l "or face(s) from the tessellated mesh and choose the appropriate";
		text -l "tool button from the options below.";
		separator -style none;

		//--------------------------------------------------------------------------------------------------------------//
		// Select Stitch Type            																				//
		//--------------------------------------------------------------------------------------------------------------//
		
		int $X     = 0;
		int $Y1    = 1;
		int $S     = 2;
		int $K     = 3;
		int $P     = 4;
		int $SK    = 5;
		int $KP    = 6;
		int $D12K  = 7;
		int $K1Y   = 8;
		int $PY    = 9;
		int $YKY   = 10;
		int $KYK   = 11;
		int $KPK   = 12;
		int $D312P = 13;
		int $D123K = 14;

		frameLayout -label "Stitch Types" -marginHeight 10 -marginWidth 20 -collapsable 0 StitchTypes;
			rowColumnLayout -numberOfColumns 4;
				symbolButton -h 80 -w 80 -image ($path + "tri1big2.bmp") -command ("ChangeStitchTypeCmd -s " + $X     + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "tri2big2.bmp") -command ("ChangeStitchTypeCmd -s " + $Y1    + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "sq1big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $S     + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "sq2big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $K     + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "sq3big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $P     + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "pt1big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $SK    + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "pt2big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $KP    + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "pt3big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $D12K  + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "pt4big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $K1Y   + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "pt5big2.bmp")  -command ("ChangeStitchTypeCmd -s " + $PY    + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "hex1big2.bmp") -command ("ChangeStitchTypeCmd -s " + $YKY   + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "hex2big2.bmp") -command ("ChangeStitchTypeCmd -s " + $KYK   + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "hex3big2.bmp") -command ("ChangeStitchTypeCmd -s " + $KPK   + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "hex4big2.bmp") -command ("ChangeStitchTypeCmd -s " + $D312P + " -n " + $nodeName);
				symbolButton -h 80 -w 80 -image ($path + "hex5big2.bmp") -command ("ChangeStitchTypeCmd -s " + $D123K + " -n " + $nodeName);
			setParent ..;
		setParent ..;

		//--------------------------------------------------------------------------------------------------------------//
		// Set Cable Edges   																							//
		//--------------------------------------------------------------------------------------------------------------//

		frameLayout -label "Set Cable Edges" -marginHeight 10 -marginWidth 10 -collapsable 0 CableEdges;
			columnLayout -columnWidth 200;
				button -l "Set Edges";
			setParent ..;
		setParent ..;

		//--------------------------------------------------------------------------------------------------------------//
		// Split/Collapse Edges  																						//
		//--------------------------------------------------------------------------------------------------------------//

		frameLayout -label "Split or Collapse Edges" -marginHeight 10 -marginWidth 10 -collapsable 0 SplitCollapseEdges;
			rowColumnLayout -numberOfRows 1;
				button -l "Split Edges";
				button -l "Collapse Edges";
			setParent ..;
		setParent ..;

		//--------------------------------------------------------------------------------------------------------------//
		// Add/Remove/Shift Edges  																						//
		//--------------------------------------------------------------------------------------------------------------//

		frameLayout -label "Add, Remove or Shift Edges" -marginHeight 10 -marginWidth 10 -collapsable 0 AddRemoveShiftEdges;
			rowColumnLayout -numberOfRows 1;
				button -l "Add Edges";
				button -l "Remove Edges" -command ("RemoveWaleEdgeCmd -n " + $nodeName);
				button -l "Shift Edges";
			setParent ..;
		setParent ..;

		//--------------------------------------------------------------------------------------------------------------//
		// Pattern Style	  																							//
		//--------------------------------------------------------------------------------------------------------------//

		frameLayout -label "Pattern Style" -marginHeight 10 -marginWidth 10 -collapsable 0 PatternStyle;
			rowColumnLayout -numberOfColumns 4;
				symbolButton -h 80 -w 80 -image ($path + "MM1b.bmp") -command "circle";
				symbolButton -h 80 -w 80 -image ($path + "MM2b.bmp") -command "sphere";
				symbolButton -h 80 -w 80 -image ($path + "MM3b.bmp") -command "polyCube";
				symbolButton -h 80 -w 80 -image ($path + "OT1b.bmp") -command "circle";
				symbolButton -h 80 -w 80 -image ($path + "OT2b.bmp") -command "sphere";
				symbolButton -h 80 -w 80 -image ($path + "OT3b.bmp") -command "polyCube";
				symbolButton -h 80 -w 80 -image ($path + "RF1b.bmp") -command "circle";
				symbolButton -h 80 -w 80 -image ($path + "RF2b.bmp") -command "sphere";
				symbolButton -h 80 -w 80 -image ($path + "RF3b.bmp") -command "polyCube";
				symbolButton -h 80 -w 80 -image ($path + "FR1b.bmp") -command "circle";
				symbolButton -h 80 -w 80 -image ($path + "FR2b.bmp") -command "sphere";
				symbolButton -h 80 -w 80 -image ($path + "FR3b.bmp") -command "polyCube";
				symbolButton -h 80 -w 80 -image ($path + "BC1b.bmp") -command "circle";
				symbolButton -h 80 -w 80 -image ($path + "BC2b.bmp") -command "sphere";
				symbolButton -h 80 -w 80 -image ($path + "BC3b.bmp") -command "polyCube";
				symbolButton -h 80 -w 80 -image ($path + "CW1b.bmp") -command "circle";
				symbolButton -h 80 -w 80 -image ($path + "CW2b.bmp") -command "sphere";
				symbolButton -h 80 -w 80 -image ($path + "CW3b.bmp") -command "polyCube";
			setParent ..;
			button -l "Generate";
		setParent ..;

	button -label "Commit Edits, Relax, and Generate Yarns" -enable false -c $commitCmd CommitEditButton;
	button -label "Clear Edits and Return to Tessellation" -enable false -c $returnCmd ReturnEditButton;

	setParent ..;
}

//======================================================================================================================//
// Stitch Editing Update Function																						//
//======================================================================================================================//

global proc AEStitchEditUpdate (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

	// dim/undim attribute fields based on nodeStage
	int $stage = `getAttr ($nodeName + ".nodeStage")`;
	if ($stage == 2) {
		button -e -enable true CommitEditButton;
		button -e -enable true ReturnEditButton;
		frameLayout -e -enable true StitchLevelEditing;
	} else {
		button -e -enable false CommitEditButton; 
		button -e -enable false ReturnEditButton;
		frameLayout -e -enable false StitchLevelEditing;
	}
	
	string $returnCmd = "showHidden pCylinder1; \
						 setAttr " + $nodeName + ".nodeStage 1; \
						 frameLayout -e -enable false StitchLevelEditing; \
						 button -e -enable true CommitTessellationButton; \
					     button -e -enable true ReturnTessellationButton; \
					     button -e -enable false CommitEditButton; \
					     button -e -enable false ReturnEditButton; \
						 editorTemplate -dimControl " + $nodeName + " stitchSize false; \
						 hide pCylinder1; showHidden StitchMeshShape;";

	button -e -c $returnCmd ReturnEditButton;
}
		

//======================================================================================================================//
// MAIN AETEMPLATE FUNCTION																								//
//======================================================================================================================//

global proc AEStitchMeshNodeTemplate( string $nodeName ) {

	editorTemplate -beginScrollLayout;
	editorTemplate -suppress "inputMesh";
	editorTemplate -suppress "outputMesh";
	editorTemplate -suppress "nodeStage";
	editorTemplate -suppress "caching";
	editorTemplate -suppress "nodeState";
	editorTemplate -suppress "inputMeshName";
	editorTemplate -suppress "outputMeshName";

	//--------------------------------------------------------------------------------------------------------------//
	// edge loop selection stage
	//--------------------------------------------------------------------------------------------------------------//
	editorTemplate -beginLayout "Edge Loop Selection" -collapse false;
		editorTemplate -callCustom "AELoopSelectionBuild" "AELoopSelectionUpdate" $nodeName;
	editorTemplate -endLayout;
	editorTemplate -addSeparator;

	//--------------------------------------------------------------------------------------------------------------//
	// tessellation stage
	//--------------------------------------------------------------------------------------------------------------//
	editorTemplate -beginLayout "Tessellation" -collapse false;
		editorTemplate -addControl "stitchSize";
		editorTemplate -callCustom "AETessellationBuild" "AETessellationUpdate" $nodeName;
	editorTemplate -endLayout;
	editorTemplate -addSeparator;
	
	//--------------------------------------------------------------------------------------------------------------//
	// stitch mesh editing stage
	//--------------------------------------------------------------------------------------------------------------//
	editorTemplate -beginLayout "Stitch Level Editing" -collapse false;
		editorTemplate -callCustom "AEStitchEditBuild" "AEStitchEditUpdate" $nodeName;
	editorTemplate -endLayout;
	editorTemplate -addSeparator;

	editorTemplate -endScrollLayout;
}